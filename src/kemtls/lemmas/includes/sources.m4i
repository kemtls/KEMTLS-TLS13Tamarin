lemma_cert_req_origin/* [typing]:
  "All certificate_request_context certificate_extensions keys #i.
    KU(senc{handshake_record('13', certificate_request_context, certificate_extensions)}keys)@i ==> 
      (Ex #j. KU(certificate_request_context)@j & #j < #i) |
      (Ex #j tid actor role. running(CertReqCtxt, actor, role, certificate_request_context)@j & #j < #i)"
*/

lemma_nst_source/* [typing]:
  "All ticket ticket_age_add tkt_lt tkt_exts app_key #i.
    KU(senc{handshake_record('4', tkt_lt, ticket_age_add, ticket, tkt_exts)}app_key)@i ==>
      (Ex #j #k. KU(ticket)@j & KU(ticket_age_add)@k & #j < #i & #k < #i) |
      (Ex tid S #j. running_server(NST, ticket, ticket_age_add)@j & #j < #i)"
*/

lemma ekem_source [sources, heuristic=s]:
  "All tid esk actor #j.
    RevEKemSk(tid, actor, esk)@j 
    ==> 
      (Ex #i. EKem(tid, actor, esk)@i & #i < #j)"

lemma revltk_source [sources, heuristic=s]:
  "All a #j.
    RevLtk(a)@j ==> Ex ltk #i. GenLtk(a, ltk)@i & #i < #j"

ifdef(<!PSK!>, <!
lemma revpsk_source [sources]:
  "All a psk #k. 
    RevealPSK(actor, psk)@#k ==> Ex ticket #j. (FreshPSK(ticket,psk)@#j & #j < #k)"
!>, <!
dnl lemma revpsk_source [sources]:
dnl  "All a psk #k. 
dnl    RevealPSK(a, psk)@#k ==> F"
!>)

lemma ephemeral_isnt_ltk [sources, heuristic=s]:
  "All tid actor actor2 esk #i #j.
    EKem(tid, actor, esk)@#i & GenLtk(actor2, esk)@#j ==> F"

lemma kemencapsulation_origin [sources, heuristic=s]:
  "All kem ciphertext ss pk keys #i. 
    KU(senc{KemEncapsulation}keys)@i &
    ciphertext = kemencaps(kem, ss, pk)
    ==>
      (Ex #j. KU(ss)@j & #j < #i) |
      (Ex #j. KemEncap(kem, pk, ss)@j & #j < #i)"

lemma kemencap_source [sources, heuristic=c]:
  "All kem pk sk ss #j.
    pk = kempk(kem, sk) &
    KemEncap(kem, pk, ss)@j
    ==>
      (Ex #i. KU(sk)@i & #i < #j) | 
      (Ex tid a #i. EKem(tid, a, sk)@i & #i < #j) | 
      (Ex a #i. GenLtk(a, sk)@i & #i < #j)"

lemma kemencap_source_pk [sources, heuristic=s]:
  "All kem certificate sk ss #j.
    certificate = kempk(kem, sk) &
    KemEncap(kem, certificate, ss)@j
    ==>
      (Ex #ch. (KU(Extension('40', kem, certificate))@#ch & #ch < #j)) |
      (Ex actor certificate_request_context keys #kg #crt. GenLtk(actor, sk)@#kg & KU(senc{Certificate}keys)@#crt & #crt < #j)"

lemma kemdecap_source_ct [sources, heuristic=s]:
  "All kem ciphertext sk ss #j.
    KemDecaps(kem, sk, ss)@j &
    ciphertext = kemencaps(kem, ss, kempk(kem, sk))
    ==>
      (Ex #sh. KU(Extension('40', kem, ciphertext))@#sh & #sh < #j) |
      (Ex keys act #kg #enc. GenLtk(act, sk)@#kg & KU(senc{KemEncapsulation}keys)@#enc & #enc < #j)"

lemma kemdecap_source_sk [heuristic=c]:
  "All kem sk ss #j.
    KemDecaps(kem, sk, ss)@j
    ==>
      (Ex tid a #i. EKem(tid, a, sk)@i & #i < #j) |
      (Ex a #l. GenLtk(a, sk)@l & #l < #j)"

lemma client_finished_running [sources, heuristic=s]:
    "
    All keys verify_data ms messages #fin.
    verify_data = compute_finished(client) &
    KU(senc{Finished}keys)@#fin
    ==>
        (Ex #kms #msgs. KU(ms)@#kms & KU(messages)@#msgs & #kms < #fin & #msgs < #fin) |
        (Ex tid actor #run. running(Transcript, actor, 'client', messages)@#run)
    "

lemma server_finished_running [sources, heuristic=s]:
    "
    All keys verify_data ms messages #fin.
    verify_data = compute_finished(server) &
    KU(senc{Finished}keys)@#fin
    ==>
        (Ex #kms #msgs. KU(ms)@#kms & KU(messages)@#msgs & #kms < #fin & #msgs < #fin) |
        (Ex tid actor #run. running(Transcript, actor, 'server', messages)@#run)
    "

lemma source_recvstream [sources]:
  "All tid actor peer auth_status data #recv.
    RecvData(tid, actor, peer, auth_status, data)@#recv
    ==>
      (Ex #pred. Recv(tid)@#pred & #pred < #recv) |
      (Ex #creac. C3b(tid)@#creac & #creac < #recv) |
      (Ex #sfin. S3fin(tid)@#sfin & #sfin < #recv)
  "

lemma source_sendstream [sources]:
  "All tid actor peer auth_status data #send.
    SendData(tid, actor, peer, auth_status, data)@#send
    ==>
      (Ex #pred. Send(tid)@#pred & #pred < #send) |
      (Ex #creac. C3a(tid)@#creac & #creac < #send) |
      (Ex #creanocrt. C3_skip_cert(tid)@#creanocrt & #creanocrt < #send) |
      (Ex #sfin. S3fin(tid)@#sfin & #sfin < #send)
  "

lemma source_auth_status [sources]:
  "All tid actor role peer pas #i.
    commit(Identity, actor, role, peer, <'auth', pas>)@#i
    ==>
      (role = 'server' & Ex #j. S2e(tid)@#j & #j < #i) |
      (role = 'client' & (Ex #c. C3a(tid)@#c & #c < #i) & (Ex #req. C2c_req(tid)@#req & #req < i))
  "

lemma source_auth_status_peer [sources]:
  "All tid actor role peer aas #i.
    commit(Identity, actor, role, peer, <aas, 'auth'>)@#i
    ==>
      (role = 'server' & (Ex #j. S3_cert(tid)@#j & #j < #i) & (Ex #req. S2c_req(tid)@#req & #req < #i)) |
      (role = 'client' & (Ex #c. C2d(tid)@#c & #c < #i))
  "

lemma unique_nonces_source [sources]:
  "All tid tid2 actor actor2 role nonces #i #j.
      commit(Nonces, actor, role, nonces)@#i &
      commit2(Nonces, actor2, role, nonces)@#j
      ==> #i = #j"

lemma unique_running_nonces_source [sources]:
  "All tid tid2 actor actor2 role nonces #i #j.
      running(Nonces, actor, role, nonces)@#i &
      running2(Nonces, actor2, role, nonces)@#j
      ==> #i = #j"

lemma unique_transcripts [sources]:
  "All tid tid2 actor actor2 role transcript #i #j.
    commit(Transcript, actor, role, transcript)@#i &
    commit2(Transcript, actor2, role, transcript)@#j
    ==> #i = #j"

lemma unique_transcripts_running [sources]:
  "All tid tid2 actor actor2 role transcript #i #j.
    running(Transcript, actor, role, transcript)@#i &
    running2(Transcript, actor2, role, transcript)@#j
    ==> #i = #j"

dnl vim: set ft=spthy ts=4 sw=4 tw=0 et :
